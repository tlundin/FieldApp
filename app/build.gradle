apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'

def localProps = new Properties()
def lpFile = rootProject.file("local.properties")
if (lpFile.exists()) {
    lpFile.withInputStream { localProps.load(it) }
}

def mapboxToken = localProps.getProperty("MAPBOX_ACCESS_TOKEN") ?: System.getenv("MAPBOX_ACCESS_TOKEN") ?: ""

android {
    defaultConfig {
        buildConfigField "String", "MAPBOX_ACCESS_TOKEN", "\"${mapboxToken}\""
    }
    signingConfigs {
        config {
            // Load signing credentials from local.properties (not checked into git)
            // Add these to your local.properties file:
            // SIGNING_KEY_ALIAS=your_key_alias
            // SIGNING_KEY_PASSWORD=your_key_password
            // SIGNING_STORE_FILE=path/to/your/keystore
            // SIGNING_STORE_PASSWORD=your_store_password
            keyAlias localProps.getProperty("SIGNING_KEY_ALIAS") ?: System.getenv("SIGNING_KEY_ALIAS")
            keyPassword localProps.getProperty("SIGNING_KEY_PASSWORD") ?: System.getenv("SIGNING_KEY_PASSWORD")
            def storeFilePath = localProps.getProperty("SIGNING_STORE_FILE") ?: System.getenv("SIGNING_STORE_FILE")
            storeFile storeFilePath ? file(storeFilePath) : null
            storePassword localProps.getProperty("SIGNING_STORE_PASSWORD") ?: System.getenv("SIGNING_STORE_PASSWORD")
        }
    }
    compileSdkVersion 35
    defaultConfig {
        applicationId "com.teraim.fieldapp"
        minSdkVersion 29  //Change back to 24
        targetSdkVersion 35
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
        coreLibraryDesugaringEnabled = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.config
        }
    }
    packaging {
        jniLibs {
            useLegacyPackaging = false
        }
    }
    namespace = 'com.teraim.fieldapp'
    buildFeatures {
        buildConfig = true
    }
    kotlinOptions {
        jvmTarget = '17'
    }
}

dependencies {
    implementation 'com.squareup.okhttp3:okhttp:5.1.0'
    implementation 'com.android.volley:volley:1.2.1'
    implementation files('libs/achartengine-1.2.0.jar')
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'com.google.android.gms:play-services-vision:20.1.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'com.google.android.gms:play-services-location:21.3.0'
    implementation 'androidx.fragment:fragment:1.8.8'
    implementation 'com.google.code.gson:gson:2.13.1'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    implementation 'com.google.android.gms:play-services-maps:19.2.0'
    implementation 'androidx.core:core-ktx:1.16.0'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.10.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.10.2'
    // Mapbox Maps SDK - using NDK 27 artifact for 16KB page size support
    // See: https://docs.mapbox.com/android/maps/guides/
    implementation 'com.mapbox.maps:android-ndk27:11.17.1'
}
